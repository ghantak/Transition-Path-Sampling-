!* sodium and chloride addition; @ICL CL- @INA NA+ MIN DIST @MND
!* excluding @SOL
!*

! sol   particles to exclude; atom selection w/o SELE and END
! icl   no. of chlorides
! ina   no. of sodiums
! mnd   min distance to another solute particle

! INITIALIZE CHLORIDES; COORDS SET 0,0,0
read sequ cla @ICL
generate cl noang nodihe first none last none
coor set xdir 0.0 ydir 0.0 zdir 0.0 sele segid cl end

! PICK A WATER MOLECULE AND REPLACE IT WITH A CHLORIDE ION
set k 1
label ilp
define xcld sele segid MYH7 .or. segid ATP .or. segid QMRE .or. segid CL end
define prox sele xcld .around. @MND end ! NEARBY EXCLUDED ATOMS
define list sele type OH2 .and. .not. prox end
calc in int( ?RAND * ?NSEL ) ! RANDOM INTEGER, BASED ON WATER COUNT
if in .lt. 1 set in ?NSEL ! CHECK FOR ZERO, CHANGE TO MAX VALUE
define targ sele list .subset. @IN end ! PICK WATER VIA RANDOM INDEX
coor stat sele targ end ! GET OH2 ATOM COORDS, ASSIGN TO CHLORIDE
coor set xdir ?XAVE ydir ?YAVE zdir ?ZAVE sele atom CL @K CLA end
delete atom sele .byres. targ end sort ! REMOVE THE WATER MOLECULE
incr k by 1
if k .le. @ICL goto ilp


! INITIALIZE SODIUMS, REPEAT ABOVE PROCESS
read sequ pot @INA
generate pot noang nodihe first none last none
coor set xdir 0.0 ydir 0.0 zdir 0.0 sele segid pot end

! NOTE THAT BOTH SEGID CL AND SEGID NA ARE 'EXCLUDED'
! NEARBY WATERS ARE REALLY EXCLUDED; REPLACE ONLY 'BULK' WATERS
set k 1
label klp
define xcld sele segid MYH7 .or. segid ATP .or. segid QMRE .or. segid POT end

define prox sele xcld .around. @MND end
define list sele type OH2 .and. .not. prox end
calc in int( ?RAND * ?NSEL )
if in .lt. 1 set in ?NSEL
define targ sele list .subset. @IN end
coor stat sele targ end
coor set xdir ?XAVE ydir ?YAVE zdir ?ZAVE sele atom POT @K POT end
delete atom sele .byres. targ end sort
incr k by 1
if k .le. @INA goto klp

return

